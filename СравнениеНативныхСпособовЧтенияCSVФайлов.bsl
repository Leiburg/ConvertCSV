#Область ЧтениеАвтоматомПоПравилам_1_2_3_4_5_6_7

&НаСервереБезКонтекста
Функция ПрочитатьCSVФайлВТаблицу(ИмяФайла, Разделитель=";", КоличествоПервыхСтрокПропустить = 0, КоличествоКолонок = Неопределено)
        
    ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла, КодировкаТекста.ANSI ,,,Ложь);
    
    ТЗ = Новый ТаблицаЗначений;
    Колонки = ТЗ.Колонки;
    ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка");
    
    Если КоличествоКолонок <> Неопределено Тогда
        Для НомерКолоки = 1 По КоличествоКолонок Цикл
            Колонки.Добавить("Колонка"+НомерКолоки, ОписаниеТиповСтрока);
        КонецЦикла;
    КонецЕсли;
    
    НомерСтроки = 1 + КоличествоПервыхСтрокПропустить;
    Стр = ЧтениеТекста.ПрочитатьСтроку();
    Пока Стр <> Неопределено Цикл
        Если НомерСтроки < 1 + КоличествоПервыхСтрокПропустить Тогда
            Стр = ЧтениеТекста.ПрочитатьСтроку();
            Продолжить;
        КонецЕсли; 
        СтрокаТЗ = ТЗ.Добавить();
        НомерПоля = 0;
        Пока Стр <> "" Цикл
            Токен = "";
            ПозицияРазделителя = Найти(стр, Разделитель);
            ПозицияОткрКавычек = Найти(стр, """");
            //"";""
            Если ПозицияОткрКавычек > 1 И Сред(стр, ПозицияОткрКавычек-1, 1) <> Разделитель Тогда
                ПозицияОткрКавычек = 0;
            КонецЕсли;
            Если (ПозицияРазделителя > ПозицияОткрКавычек ИЛИ ПозицияРазделителя = 0) И ПозицияОткрКавычек > 0 Тогда
                // начинающееся с кавычек читаем до тех пор
                Токен = Лев(Стр, ПозицияОткрКавычек);
                Стр = Сред(Стр, ПозицияОткрКавычек+1);
                ПозицияДляПоискаЗакрКавычек = 1;
                ПозицияЗакрКавычек = 0;
                Пока ПозицияЗакрКавычек = 0 Цикл
                    Если СтрДлина(Стр) >= ПозицияДляПоискаЗакрКавычек Тогда
                    ПозицияЗакрКавычек = СтрНайти(Стр, """",,ПозицияДляПоискаЗакрКавычек);
                    КонецЕсли;
                    
                    Если ПозицияЗакрКавычек > 0 И Сред(Стр,ПозицияЗакрКавычек + 1, 1) = """" Тогда
                        ПозицияДляПоискаЗакрКавычек = ПозицияЗакрКавычек + 2;
                        ПозицияЗакрКавычек = 0;
                        Продолжить; //это просто экранированная кавычка, а не кавычка закрывающее поле
                    ИначеЕсли ПозицияЗакрКавычек > 0 Тогда
                        Прервать;
                    Иначе
                        Токен = Токен + Стр + Символы.ПС;
                        НомерСтроки = НомерСтроки + 1;
                        Стр = ЧтениеТекста.ПрочитатьСтроку();
                        Если Стр = Неопределено Тогда
                            Стр = "";
                            Прервать; // файл закончился, закрывающую кавычку не нашли
                        КонецЕсли;         
                        ПозицияДляПоискаЗакрКавычек = 1; // ищем с начала новой, полученной строки
                    КонецЕсли;
                                        
                КонецЦикла;
                
                Если СтрДлина(Стр) > ПозицияЗакрКавычек Тогда 
                    ПозицияРазделителя=СтрНайти(Стр, Разделитель,,ПозицияЗакрКавычек + 1);
                Иначе
                    ПозицияРазделителя = 0;
                КонецЕсли;
            КонецЕсли;
            
            Если ПозицияРазделителя>0 Тогда
                Токен = Токен + Лев(Стр, ПозицияРазделителя-1);
                Стр = Сред(Стр, ПозицияРазделителя+1);
            Иначе
                Токен = Токен + Стр; // разделитель не нашли, добавляем оставшуюся часть строки
                Стр = ""; // строка полностью обработана
            КонецЕсли;
            
            // Уберем экранирующие кавычки у поля, если они там были
            Если Лев(Токен, 1) = """" Тогда
                Токен = Сред(Токен, 2);
                Токен = ?(Прав(Токен, 1) = """", Лев(Токен, СтрДлина(Токен)-1), Токен);
            КонецЕсли;
                        
            // убираем двойные кавычки 
            Токен = СтрЗаменить(Токен, """""", """");
            
            НомерПоля = НомерПоля + 1;
            Если КоличествоКолонок = Неопределено Тогда
                Если Колонки.Количество()<НомерПоля Тогда
                    Колонки.Добавить("Колонка"+НомерПоля, ОписаниеТиповСтрока);
                КонецЕсли;
            ИначеЕсли НомерПоля > КоличествоКолонок Тогда
                Прервать;
            КонецЕсли;
            СтрокаТЗ[НомерПоля-1] = Токен;
            
        КонецЦикла;
        НомерСтроки = НомерСтроки + 1;
        Стр = ЧтениеТекста.ПрочитатьСтроку();
    КонецЦикла;
    
    ЧтениеТекста.Закрыть();
    
    Возврат ТЗ; 
КонецФункции

&НаСервере
Процедура Команда3НаСервере(Текст)
	КолСтрок = СтрЧислоСтрок(Текст);
	т1 = ТекущаяУниверсальнаяДатаВМиллисекундах();
	//ТаблицаРезультат = ПреобразоватьТекстCSVвТЗ(Текст); // (40, 43, 40.816) на 10тыс => 1тыс за 4сек
	ТаблицаРезультат = ПрочитатьCSVФайлВТаблицу(ИмяФайла);
	т2 = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Сообщить(т1);
	Сообщить(т2);
	Сообщить("с кавычками, мс:" + (т2-т1));
	Сообщить("с кавычками, сек:" + (т2-т1)/1000);
	Сообщить("с кавычками, сек на 1тыс:" + (т2-т1)/1000 / (КолСтрок/1000));
	Сообщить("с кавычками, строк в секунду:" + КолСтрок/(т2-т1)*1000);
КонецПроцедуры


&НаКлиенте
Процедура Команда3(Команда)
	ТекстДок = Новый ТекстовыйДокумент;
	ТекстДок.Прочитать(ИмяФайла);
	Текст = ТекстДок.ПолучитьТекст();
	Команда3НаСервере(Текст);
КонецПроцедуры

#КонецОбласти

#Область ЛинейноеЧтениеПоПравилам_1_2_3_4

&НаСервере
Функция ПрочитатьCSVвТЗ(ИмяФайла, Разделитель=";", ЗаголовкиИзПервойСтроки = Ложь)
	Текст = Новый ЧтениеТекста(ИмяФайла);
	
	Результат = Новый ТаблицаЗначений;
	
	// создадим колонки
	ТекСтрока = Текст.ПрочитатьСтроку();
	Если ТекСтрока <> Неопределено Тогда
		МассивЗначений = СтрРазделить(ТекСтрока, Разделитель);
		ИндексКолонки = 0;
		Для Каждого ИмяКолонки Из МассивЗначений Цикл
			ИмяКолонки = ?(ЗаголовкиИзПервойСтроки, "Кол"+ИндексКолонки, ИмяКолонки);
			Результат.Колонки.Добавить("Колонка" + ИндексКолонки, , ИмяКолонки);
			ИндексКолонки = ИндексКолонки + 1;
		КонецЦикла;
		Если ЗаголовкиИзПервойСтроки Тогда
			ТекСтрока = Текст.ПрочитатьСтроку();
		КонецЕсли;
	КонецЕсли;

	Пока ТекСтрока <> Неопределено Цикл // строки читаются до символа перевода строки
		НоваяСтрока = Результат.Добавить();
		
		МассивЗначений = СтрРазделить(ТекСтрока, Разделитель);
		ИндексКолонки = 0;
		Для Каждого ТекЗначение Из МассивЗначений Цикл
			НоваяСтрока[ИндексКолонки] = ТекЗначение;
			ИндексКолонки = ИндексКолонки + 1;
		КонецЦикла;

		ТекСтрока = Текст.ПрочитатьСтроку();
	КонецЦикла;
		
	Возврат Результат;
КонецФункции

&НаСервере
Процедура Команда4НаСервере()
	ТекстДок = Новый ТекстовыйДокумент;
	ТекстДок.Прочитать(ИмяФайла);
	Текст = ТекстДок.ПолучитьТекст();
	КолСтрок = СтрЧислоСтрок(Текст);
	
	// Вставить содержимое обработчика.
	т1 = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ТаблицаРезультат = ПрочитатьCSVвТЗ(ИмяФайла);
	т2 = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Сообщить("начало: " + т1);
	Сообщить("конец: " + т2);
	Сообщить("строк: " + КолСтрок);
	Сообщить("без кавычек, мс:" + (т2-т1));
	Сообщить("без кавычек, сек:" + (т2-т1)/1000);
	Сообщить("без кавычек, сек на 1тыс:" + (т2-т1)/1000 / (КолСтрок/1000));
	Сообщить("без кавычек, строк в секунду:" + КолСтрок/(т2-т1)*1000);

КонецПроцедуры

&НаКлиенте
Процедура Команда4(Команда)
	Команда4НаСервере();
КонецПроцедуры

#КонецОбласти

#Область ЧтениеЭвристикойArxxximedПоПравилам_1_2_3_4_5_6_7

&НаСервере
Функция CSV_Reader(ЧтениеТекста,Разделитель = ";")  
	СтрокаТекста = ЧтениеТекста.ПрочитатьСтроку();
	Если СтрокаТекста = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	КолКавычек = СтрЧислоВхождений(СтрокаТекста,"""");
	Если КолКавычек%2 Тогда
		МассивСложения = Новый Массив;
		МассивСложения.Добавить(СтрокаТекста);
		Пока КолКавычек%2 Цикл
			СтрокаТекста = ЧтениеТекста.ПрочитатьСтроку();
			КолКавычек = КолКавычек + СтрЧислоВхождений(СтрокаТекста,"""");
			МассивСложения.Добавить(СтрокаТекста);
		КонецЦикла;
		СтрокаТекста = СтрСоединить(МассивСложения,Символы.ПС);
	КонецЕсли;
	Если КолКавычек = 0 Тогда
		Возврат СтрРазделить(СтрокаТекста,Разделитель,Истина);
	КонецЕсли; 
	
	МассивПодстрок = СтрРазделить(СтрокаТекста,Разделитель,Истина);
	МассивЯчеек = Новый Массив;
	Сч = 0; КоличествоЭл = МассивПодстрок.Количество();
	Пока Сч <КоличествоЭл Цикл
		Подстрока = МассивПодстрок[Сч];	
		КолКавычек = СтрЧислоВхождений(Подстрока,"""");
		Если КолКавычек%2 Тогда
			МассивСложения = Новый Массив;
			МассивСложения.Добавить(Подстрока);
			Пока КолКавычек%2 Цикл
				Сч =Сч +1;
				Подстрока = МассивПодстрок[Сч];
				КолКавычек = КолКавычек + СтрЧислоВхождений(Подстрока,"""");
				МассивСложения.Добавить(Подстрока);
			КонецЦикла;
			Подстрока = СтрСоединить(МассивСложения,Разделитель);
		КонецЕсли;
		Если КолКавычек <> 0 Тогда	
			Подстрока = Сред(Подстрока,2,СтрДлина(Подстрока)-2);
			Подстрока = СтрЗаменить(Подстрока,"""""","""");	
		КонецЕсли;
		МассивЯчеек.Добавить(Подстрока);
		Сч =Сч +1;	
	КонецЦикла;
	
	Возврат МассивЯчеек;	
	
КонецФункции

&НаСервере
Функция ПолучитьТаблицуДанныхЧерезREADeR(ИмяФайла_CSV,Разделитель = ";",ПерваяСтрокаСодержитЗаголовки = Истина) 
	ТаблицаЗначений = Новый ТаблицаЗначений;
	
	ЧтениеТекста = Новый ЧтениеТекста(ИмяФайла_CSV,"windows-1251"); // или "UTF-8"
	//Замер производительности
	Старт = ТекущаяУниверсальнаяДатаВМиллисекундах();
	//}}
	
	МассивПолейCSV = CSV_Reader(ЧтениеТекста);
	Если МассивПолейCSV = Неопределено Тогда
		Возврат ТаблицаЗначений;	
	КонецЕсли;
	// Устанавливаем колонки ТаблицыЗначений
	Если ПерваяСтрокаСодержитЗаголовки Тогда
		Для Каждого ПолеCSV Из МассивПолейCSV Цикл
			ТаблицаЗначений.Колонки.Добавить(ПолеCSV);
		КонецЦикла;
		МассивПолейCSV = CSV_Reader(ЧтениеТекста);// Читаем вторую строку
		
	Иначе
		Для Каждого ПолеCSV Из МассивПолейCSV Цикл
			ИндексКолонки = ТаблицаЗначений.Колонки.Количество(); 
			ТаблицаЗначений.Колонки.Добавить("Колонка_" + Формат(ИндексКолонки,"ЧН=; ЧГ=")); 
		КонецЦикла;		
		
	КонецЕсли;
	
	КоличествоКолонокТЗ = ТаблицаЗначений.Колонки.Количество(); 
	Пока МассивПолейCSV <> Неопределено Цикл
		СтрокаТЗ = ТаблицаЗначений.Добавить();
		ИндексКолонки = 0;
		Для Каждого ПолеCSV Из МассивПолейCSV Цикл 
			Если ИндексКолонки = КоличествоКолонокТЗ Тогда
				ТаблицаЗначений.Колонки.Добавить("Колонка_" + Формат(ИндексКолонки,"ЧН=; ЧГ="));
				КоличествоКолонокТЗ = КоличествоКолонокТЗ +1;
			КонецЕсли;
			СтрокаТЗ[ИндексКолонки] = ПолеCSV;
			ИндексКолонки = ИндексКолонки +1;
		КонецЦикла;
		
		МассивПолейCSV = CSV_Reader(ЧтениеТекста);
	КонецЦикла;
	
	//Замер производительности{{
	финиш = ТекущаяУниверсальнаяДатаВМиллисекундах();
	КоличествоСтрок = ТаблицаЗначений.Количество();
	//Сообщить(СтрШаблон("Потрачено: %1 сек, количество строк: %2",(финиш - Старт)/1000, КоличествоСтрок));
	//}}
	
	Возврат ТаблицаЗначений;
КонецФункции

&НаСервере
Процедура Команда5НаСервере()
	ТекстДок = Новый ТекстовыйДокумент;
	ТекстДок.Прочитать(ИмяФайла);
	Текст = ТекстДок.ПолучитьТекст();
	КолСтрок = СтрЧислоСтрок(Текст);
	
	// Вставить содержимое обработчика.
	т1 = ТекущаяУниверсальнаяДатаВМиллисекундах();
	ТаблицаРезультат = ПолучитьТаблицуДанныхЧерезREADeR(ИмяФайла);
	т2 = ТекущаяУниверсальнаяДатаВМиллисекундах();
	Сообщить("начало: " + т1);
	Сообщить("конец: " + т2);
	Сообщить("строк: " + КолСтрок);
	Сообщить("эвристикой, мс:" + (т2-т1));
	Сообщить("эвристикой, сек:" + (т2-т1)/1000);
	Сообщить("эвристикой, сек на 1тыс:" + (т2-т1)/1000 / (КолСтрок/1000));
	Сообщить("эвристикой, строк в секунду:" + КолСтрок/(т2-т1)*1000);
КонецПроцедуры

&НаКлиенте
Процедура Команда5(Команда)
	Команда5НаСервере();
КонецПроцедуры

#КонецОбласти

// РЕЗУЛЬТАТЫ
//
// Cравнение способов чтения средствами 1С:
//  - линейное по правилам 1,2,3,4 читает со скоростью - 10 000 строк / 1 сек
//  - автомат по правилам 1-7 (с кавычками и переносами) - 1000 строк / 1 сек
//  - эвристика Arxxximed по правилам 1-7 - 2300 строк / 1 сек
